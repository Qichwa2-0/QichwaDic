apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.ocram.qichwadic"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 17
        versionName "3.0.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
        buildConfigField "boolean", "DEBUGGABLE", 'false'
        buildConfigField "String", "SERVER_URL", '"https://www.api.qichwa.net/simitaqi/"'
        buildConfigField "String", "DEV_EMAIL", '"qichwa2.0@gmail.com"'
    }
    buildTypes {
        debug{
            buildConfigField "boolean", "DEBUGGABLE", 'true'
            buildConfigField "String", "SERVER_URL", '"https://www.api.qichwa.net/simitaqi/"'
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {

    // Support
    implementation deps.support.appcompat
    implementation deps.support.constraint
    implementation deps.support.cardview
    implementation deps.support.supportv4
    implementation deps.support.core
    implementation deps.support.design
    implementation deps.support.preferencev7
    implementation deps.support.preferencev14

    // Architecture components
    implementation deps.arch.room
    implementation deps.arch.roomTesting
    implementation deps.arch.roomRx
    implementation deps.arch.liveDataViewModel
    implementation deps.arch.reactiveStreams

    annotationProcessor deps.arch.roomAnnotation
    annotationProcessor deps.arch.lifecycleAnnotation

    // Square related deps
    implementation deps.square.retrofit
    implementation deps.square.adapter_rx_java
    implementation deps.square.converter_gson
    implementation deps.square.butterknife
    debugImplementation deps.square.leakCanaryDebug
    releaseImplementation deps.square.leakCanaryRelease

    annotationProcessor deps.square.butterknifeProcessor

    // rx
    implementation deps.rx.rxjava
    implementation deps.rx.rxandroid

    // testing
    androidTestImplementation deps.testing.coretesting
    androidTestImplementation deps.testing.androidxtestcore
    androidTestImplementation deps.testing.androidxjunit
    androidTestImplementation deps.testing.rules

    // other deps
    implementation deps.appintro.appintro
    implementation deps.pagersliding.pagersliding

    // Resolve conflicts between main and test APK:
    androidTestImplementation deps.support.annotations
    androidTestImplementation deps.support.supportv4
    androidTestImplementation deps.support.appcompat
    androidTestImplementation deps.support.design

    implementation deps.dagger.dagger
    implementation deps.dagger.dagger_support // if you use the support libraries
    annotationProcessor deps.dagger.dagger_compiler
    annotationProcessor deps.dagger.dagger_android_processor
}